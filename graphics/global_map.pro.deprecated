PRO GLOBAL_MAP, LONGITUDE     = longitude,     $
                MAPLIMIT      = maplimit,      $
                LONDEL        = lonDel,        $
                LATDEL        = latDel,        $
                LATLABEL      = latLabel,      $
                LONLABEL      = lonLabel,      $
                CHARSIZE      = charsize,      $
                BOX_AXES      = box_axes,      $
                LABEL_AXES    = label_axes,    $
                OVERPLOT      = overplot,      $
                MAPSET        = mapset,        $
                ADVANCE       = advance,       $
                POSITION      = position,      $
                PROJECTION    = projection,    $
                FUNC_GRAPHICS = FUNC_GRAPHICS, $
                TITLE         = title,         $
                USA           = usa,           $
                COUNTRIES     = countries,     $
                RIVERS        = rivers,        $
                MAP_Object    = map,           $
                MAP_FILL      = map_fill
;+
; Name:
;   GLOBAL_MAP
; Purpose:
;   To plot a map of the global with latitude lines
; Input:
;   None.
; Output:
;   None.
; Keywords:
;   LONGITUDE   : Set this to the longitudegitude that you would 
;                  like the map centered at. Default is 0.0
;   MAPLIMIT    : A four (4) element array limiting the map to plot. 
;                 Limits are:
;                    i. Element 0 - South Latitude
;                   ii. Element 1 - West longitude
;                  iii. Element 2 - North Latitude
;                   iv. Element 3 - East longitude
;   LONDEL      : This is the interval for longitude lines.
;                 Default is 30.0
;   LATDEL      : This is the interval for latitude lines.
;                 Default is 15.0
;   LATLABEL    : Longitude at which latitude labels are to be drawn.
;   LONLABEL    : Latitude at which longitude labels are to be drawn.
;   CHARSIZE    : Size of lat and lon labels
;   BOX_AXES    : Plot lat/lon labels around plot
;   MAPSET      : Set keyword to only run map set
;   FUNC        : Set to use Function graphics.
;   MAP_FILL    : Set to color to fill in continents
; Creation/History
;   Kyle R. Wodzicki	Created 16 April 2014
;
;     MODIFIED 04 June 2014:
;       Added option of lat and longitude line interval
;     MODIFIED 05 June 2014:
;       Added advance as option for P.MULTI and the postion option and
;       title option.
;     MODIFIED 25 June 2014:
;       Added CHARSIZE keyword and BOX_AXES.
;     MODIFIED 03 July 2014:
;       This was written initially for longitudes between -180 to 180.
;       Thus, added to convert back to that style
;     MODIFIED 14 July 2014:
;       Added the FUNC_GRAPHICS keyword to utilize function graphics.
;     MODIFIED 17 July 2014:
;       This iteration of the procedure has been added to the
;       pf_cgContour program for more portability. SAVEPLOT has been 
;       removed as well.
;     MODIFIED 28 July 2014:
;       Changed default latitude interval from 30 degrees to 15 
;       degrees and default longitude interval from 45 to 30 degreses.
;     MODIFIED 27 Oct. 2014:
;       Changed all functions and procedures from Coyote Graphics
;       to standard IDL calls.
;     MODIFIED 19 Nov. 2014
;       Added USA keyword
;     MODIFIED 24 Feb. 2014 by Kyle R. Wodzicki:
;       Added projection keyword, default is still cylindrical
;     MODIFIED 04 Mar. 2015 by Kyle R. Wodzicki
;       Added COUNTRIES keyword.
;     MODIFIED 16 Mar. 2015 by Kyle R. Wodzicki
;       Added RIVERS keyword.
;     MODIFIED 04 Mar. 2016 by Kyle R. Wodzicki
;       Added MAP_FILL keyword.
;-

COMPILE_OPT IDL2, HIDDEN                                              ;Set compile options

IF (N_ELEMENTS(charsize) EQ 0) THEN charsize = 1
IF ~KEYWORD_SET(title)   THEN title=' '
IF KEYWORD_SET(box_axes) THEN title = title+'!C'                      ;Add return if box_axes set

IF ~KEYWORD_SET(latDel) THEN latDel = 15.0                            ;Set to default if not set
IF ~KEYWORD_SET(lonDel) THEN lonDel = 30.0                            ;Set to default if not set

IF KEYWORD_SET(maplimit) THEN BEGIN                                   ;If maplimit set		
  IF (N_ELEMENTS(longitude) EQ 0) THEN BEGIN                               ; and longitude not set
    IF (maplimit[1] LT maplimit[3]) THEN $
      longitude = (maplimit[1] + maplimit[3])/2.0 $                   ;Calculate middle of maplimit
    ELSE BEGIN
      longitude = ((180.0-maplimit[1])-(180.0+maplimit[3]))/2.0
      longitude = (longitude LT 0) ? -180.0-longitude $
                                   :  180.0-longitude
    ENDELSE
  ENDIF
ENDIF ELSE maplimit = [-90.0, -180.0, 90.0, 180.0]                    ;Default maplimit

IF (N_ELEMENTS(projection) EQ 0) THEN projection = 'CYLINDRICAL'
IF (N_ELEMENTS(map_fill)   EQ 0) THEN $
  map_color  = COLOR_24('black') $
ELSE $
  map_color  = map_fill
;=====================================================================
; SET UP FUNCTION GRAPHICS WINDOW
IF KEYWORD_SET(func_graphics) THEN BEGIN
  map = MAP(projection, LIMIT=maplimit, $
            POSITION=position, TITLE=title)
  grid = map.MAPGRID
  grid.LONGITUDE_MIN = maplimit[1] & grid.LONGITUDE_MAX = maplimit[3] ;Set longitude limits
  grid.LATITUDE_MIN  = maplimit[0] & grid.LATITUDE_MAX  = maplimit[2] ;Set latitude limits
  grid.GRID_LATITUDE = latDel      & grid.GRID_LONGITUDE= lonDel      ;Set lat/lon line spacing
  grid.linestyle     = 'dotted'                                       ;Set line style
  grid.label_position= 0                                              ;Set label position to bottom and left
  grid.FONT_SIZE     = 8                                              ;Set font size
  map['Longitudes'].LABEL_ANGLE=0                                     ;Rotate longitude labels
  continents = MAPCONTINENTS(FILL_COLOR='light gray')                 ;Plot continents
ENDIF ELSE BEGIN
  IF KEYWORD_SET(mapset) THEN BEGIN                                   ;If just setting up map
    cgMAP_SET, 0.0, longitude, $
                NAME     = projection, $; /CYLINDRICAL, $
                LIMIT    = maplimit,   $
                POSITION = position,   $
                TITLE    = title,      $
                ADVANCE  = advance,    $
                CHARSIZE = charsize,   $
                LATDEL   = latDel,     $
                LONDEL   = lonDel
  ENDIF ELSE BEGIN                                                    ;If not only setting map 
    cgMAP_CONTINENTS, /CONTINENTS, OVERPLOT=overplot, $
      USA             = usa, $
      COUNTRIES       = countries, $
      RIVERS          = rivers,    $
      COLOR           = map_color, $
      FILL_CONTINENTS = N_ELEMENTS(map_fill) NE 0
    IF KEYWORD_SET(label_axes) THEN BEGIN
      AXIS, xAxis=0, xRANGE=maplimit[1:*:2], xTickInterval=lonDel, $
        xSTYLE=1, xTickLen=-0.01, CHARSIZE = charsize/1.5, COLOR=0
      AXIS, xAxis=1, xRANGE=maplimit[1:*:2], xTickInterval=lonDel, $
        xSTYLE=1, xTickLen=-0.01, CHARSIZE = charsize/1.5, COLOR=0, xTickFormat="(A1)"
      AXIS, yAxis=0, yRANGE=maplimit[0:*:2], yTickInterval=latDel, $
        ySTYLE=1, yTickLen=-0.01, CHARSIZE = charsize/1.5, COLOR=0
      AXIS, yAxis=1, yRANGE=maplimit[0:*:2], yTickInterval=latDel, $
        ySTYLE=1, yTickLen=-0.01, CHARSIZE = charsize/1.5, COLOR=0, yTickFormat="(A1)"
    ENDIF ELSE $
      cgMAP_GRID, LATDEL   = latDel,   $
                LONDEL   = lonDel,   $
                LATLABEL = latLabel, $
                LONLABEL = lonLabel, $
                CHARSIZE = charsize, $
                BOX_AXES = box_axes, $
                COLOR    = 0,        $
                /LABEL
  ENDELSE
ENDELSE
IF KEYWORD_SET(box_axes) THEN title = FILE_BASENAME(title, '!C')      ;Remove return character from title
END